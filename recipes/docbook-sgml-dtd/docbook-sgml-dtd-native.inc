# The DTDs of the various versions have to be installed in parallel and should
# not replace each other. The installation step is common for all versions
# and just differs in the DTD_VERSION.
#
# However the DTDs need some quirks (see LFS documentation).

DESCRIPTION = "Document type definitions for verification of SGML data files against the DocBook rule set"

DEPENDS = "sgml-common-native"

SRC_URI = "http://www.docbook.org/sgml/${DTD_VERSION}/docbook-${DTD_VERSION}.zip"

S = "${WORKDIR}"

INC_PR = "r2"

SYSROOT_PREPROCESS_FUNCS += "docbook_sgml_dtd_native_mangle"

inherit native

do_install () {
    # Refer to http://www.linuxfromscratch.org/blfs/view/stable/pst/sgml-dtd.html
    # for details.
    install -d -m 755 ${D}${datadir}/sgml/docbook/sgml-dtd-${DTD_VERSION}
    install docbook.cat ${D}${datadir}/sgml/docbook/sgml-dtd-${DTD_VERSION}/catalog
    cp -PpRr *.dtd *.mod *.dcl ${D}${datadir}/sgml/docbook/sgml-dtd-${DTD_VERSION}

    install-catalog --add ${sysconfdir}/sgml/sgml-docbook-dtd-${DTD_VERSION}.cat \
      ${D}${datadir}/sgml/docbook/sgml-dtd-${DTD_VERSION}/catalog
    install-catalog --add ${sysconfdir}/sgml/sgml-docbook-dtd-${DTD_VERSION}.cat \
      ${sysconfdir}/sgml/sgml-docbook.cat
    # Moving them to image directory so that they get included in the .ipk file
    # generated by new style staging.
    install -d ${D}${sysconfdir}/sgml
    cp ${sysconfdir}/sgml/*.cat ${D}${sysconfdir}/sgml/
    cp ${sysconfdir}/sgml/catalog ${D}${sysconfdir}/sgml/
}

PACKAGES = ""
docbook_sgml_dtd_native_mangle () {
    # Removing the image directory path (${D}) from the .cat files.
    sed -i -e "s|${D}||g" ${SYSROOT_DESTDIR}${sysconfdir}/sgml/*.cat
}
NATIVE_INSTALL_WORKS = "1"
