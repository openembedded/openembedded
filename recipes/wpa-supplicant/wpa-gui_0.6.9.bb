DESCRIPTION = "Qt interface for choosing which configured network to connect \
to. It also provides a method for browsing 802.11 SSID scan results, an event \
history log of messages generated by wpa_supplicant, and a method to add or \
edit wpa_supplicant networks."
SECTION = "network"
LICENSE = "GPL BSD"
HOMEPAGE = "http://hostap.epitest.fi/wpa_supplicant/"
DEPENDS = "librsvg-native imagemagick-native"
RDEPENDS = "wpa-supplicant"
RRECOMMENDS = "${LIBC}-gconv-utf-16"
PR = "r1"

SRC_URI = "http://hostap.epitest.fi/releases/wpa_supplicant-${PV}.tar.gz\
         file://icons.patch;patch=1 \
         file://session_manager.patch;patch=1 "

SRC_URI_append_shr = "file://desktop.patch;patch=1 \
	              file://style"

S = "${WORKDIR}/wpa_supplicant-${PV}/wpa_supplicant/wpa_gui-qt4"

inherit qt4x11
ARM_INSTRUCTION_SET = "arm"

do_compile_append() {
       oe_runmake -C ${S}/icons
}

do_install () {
       install -d ${D}${bindir}
       install -d ${D}${datadir}/wpa_gui
       install -d ${D}${datadir}/applications
       install -d ${D}${datadir}/pixmaps
       install -m 644 icons/hicolor/128x128/apps/wpa_gui.png ${D}${datadir}/pixmaps
       install -m 755 wpa_gui ${D}/${bindir}
       install -m 644 wpa_gui.desktop ${D}/${datadir}/applications
}

do_install_append_shr() {
       install -m 644 ${WORKDIR}/style ${D}/${datadir}/wpa_gui
}

FILES_${PN} = "/usr/share/pixmaps/wpa_gui.png \
               /usr/share/wpa_gui/style \
               /usr/bin/wpa_gui \
               /usr/share/applications/wpa_gui.desktop"

FILES_${PN}_append_shr = " /usr/share/wpa_gui/style"
