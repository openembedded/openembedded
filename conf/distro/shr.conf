#-----------------------------------------------------------------------------
#@TYPE: Distribution
#@NAME: SHR
#@DESCRIPTION: Linux Distribution Configuration for Mobile Phones
#-----------------------------------------------------------------------------

require conf/distro/include/preferred-om-2009-versions.inc
require conf/distro/include/preferred-om-2008-versions.inc
require conf/distro/include/angstrom-2007-for-openmoko.inc

#
# Header
#
DISTRO_NAME = "shr"
DISTRO_VERSION = "-${DATE}"
#DISTRO_TYPE = "release"
DISTRO_TYPE = "debug"
DISTRO = "shr"

OVERRIDES = "local:${MACHINE}:openmoko:angstrom:${TARGET_OS}:${TARGET_ARCH}:build-${BUILD_OS}:fail-fast:pn-${PN}"

INHERIT += "packaged-staging"

CVS_TARBALL_STASH += "http://build.shr-project.org/sources/"

PREMIRRORS = "(ftp|https?)$://.*/.* http://build.shr-project.org/sources/"

DISTRO_FEED_PREFIX ?= "shr-dev"
DISTRO_FEED_URI ?= "http://build.shr-project.org/shr-testing/ipk"

AB_DEPLOY_EXTRA ?= ""
DEPLOY_DIR_IMAGE = "${DEPLOY_DIR}/images/${MACHINE_ARCH}/${AB_DEPLOY_EXTRA}"

IMAGE_INITSCRIPTS = "initscripts-shr"

# We try to minimize the distro features in the default image.
DISTRO_FEATURES = "wifi ppp alsa ext2 vfat usbhost"

# Some features (bluetooth, usbgadget) include packages we do not like.
# Pick some of the packages manually.
DISTRO_EXTRA_RDEPENDS += " \
    bluez4 \
    kernel-module-g-ether \
    "

# 2.18 seems to break badly on 64-bit systems
PREFERRED_VERSION_binutils ?= "2.17.50.0.12"
PREFERRED_VERSION_binutils-cross ?= "2.17.50.0.12"

# Bleeding-edge developers should add the following lines to their local.conf:
# require conf/distro/include/moko-autorev.inc
# require conf/distro/include/fso-autorev.inc

# override the PACKAGE_ARCH of certain packages for openmoko distro
#
# Same kernel used to run on both these machines...
# Currently not used but left here in case someone wants to switch on
# quickly
#PACKAGE_ARCH_pn-linux-openmoko_om-gta01 = "armv4t"
#PACKAGE_ARCH_pn-linux-openmoko_om-gta02 = "armv4t"
#PACKAGE_ARCH_pn-linux-openmoko-devel_om-gta01 = "armv4t"
#PACKAGE_ARCH_pn-linux-openmoko-devel_om-gta02 = "armv4t"

# Same kernel so same usb
PACKAGE_ARCH_pn-usb-gadget-mode_om-gta01 = "armv4t"
PACKAGE_ARCH_pn-usb-gadget-mode_om-gta02 = "armv4t"
# same kernel so same keymaps
PACKAGE_ARCH_pn-keymaps_om-gta01 = "armv4t"
PACKAGE_ARCH_pn-keymaps_om-gta02 = "armv4t"
# neod knows about both machines
PACKAGE_ARCH_pn-neod_om-gta01 = "armv4t"
PACKAGE_ARCH_pn-neod_om-gta02 = "armv4t"

# Select package providers
PREFERRED_PROVIDER_gtk+ = "gtk+"
PREFERRED_PROVIDER_opkg = "opkg"
PREFERRED_PROVIDER_libgsmd = "libgsmd"

# for jalimo
PREFERRED_PROVIDER_virtual/javac-native = "ecj-bootstrap-native"
PREFERRED_PROVIDER_classpath = "classpath"
PREFERRED_PROVIDER_virtual/java-initial = "cacao-initial"
PREFERRED_PROVIDER_swt3.4-gtk = "swt3.4-gtk"
PREFERRED_PROVIDER_midpath = "midpath-openmoko"

# We don't use this, but select one to shut bitbake up.
PREFERRED_PROVIDER_virtual/libqte2 = "qte"

# Helper to say what image we built
SHR_VERSION_FILE = "${IMAGE_ROOTFS}/${sysconfdir}/shr-version"
ROOTFS_POSTPROCESS_COMMAND += "OLD_PWD=$PWD; cd `dirname '${FILE_DIRNAME}'`; echo Tag Name: `git tag|tail -n 1`> ${SHR_VERSION_FILE};cd $OLD_PWD;"
ROOTFS_POSTPROCESS_COMMAND += "OLD_PWD=$PWD; cd `dirname '${FILE_DIRNAME}'`; echo VERSION: `git-log -n1 --pretty=oneline|awk '{print $1}'` >> ${SHR_VERSION_FILE}; cd $OLD_PWD;"
ROOTFS_POSTPROCESS_COMMAND += "OLD_PWD=$PWD; cd `dirname '${FILE_DIRNAME}'`; echo Branch: ` git branch |awk '/*/{print $2}'` >> ${SHR_VERSION_FILE}; cd $OLD_PWD;"
ROOTFS_POSTPROCESS_COMMAND += "echo Build Host: `cat /etc/hostname` >> ${SHR_VERSION_FILE};"
ROOTFS_POSTPROCESS_COMMAND += "echo Time Stamp: `date -R` >> ${SHR_VERSION_FILE};"
